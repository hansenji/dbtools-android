buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'
apply plugin: 'maven'

// todo JetBrains has indicated that coroutines are stable, but the kotlinx-coroutines designs may change ... remove this when kotlinx-coroutines are no longer experimental
kotlin {
    experimental {
        coroutines 'enable' // temp disable warnings
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'org.dbtools:dbtools-query:3.0.0'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    // optional
    provided "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    provided "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    provided 'joda-time:joda-time:2.9.9'
    provided 'org.threeten:threetenbp:1.3.6'

    provided 'org.sqlite.sqliteX:sqlite-android:3.15.2' // SQL Android Bindings
    provided "com.android.support:appcompat-v7:$supportLibVersion"
    provided "com.android.support:support-vector-drawable:$supportLibVersion"
    provided "com.android.support:recyclerview-v7:$supportLibVersion" // used by paging

    provided 'net.zetetic:android-database-sqlcipher:3.5.7'
    provided "org.xerial:sqlite-jdbc:$xerialSqliteJdbcVersion"

    provided "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    provided "android.arch.lifecycle:runtime:$archLifecycleVersion"
    provided "android.arch.lifecycle:extensions:$archLifecycleVersion"
    provided "android.arch.paging:runtime:1.0.0-alpha6"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.16.0'
    testCompile 'joda-time:joda-time:2.9.9'
    testCompile 'org.threeten:threetenbp:1.3.6'
}

// =============== Maven Deployment ===============
// ./gradlew clean check installLocal
task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository (url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME
            pom.packaging = POM_PACKAGING
            uniqueVersion = false // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// DEPLOY to Maven Central (this must be the last line of this file)
// ./gradlew clean check uploadArchives
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')